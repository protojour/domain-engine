domain 01HH2687A8SPTZSX05YMBR43AH ()

def Repository (
    rel. 'id'[rel* gen: auto]: (rel* is: uuid)
    rel* 'handle': text
    rel* ownership.owner: RepositoryOwner
    rel* fork.origin?: .
)

edge ownership {
    (repo) owner: (owner),
    (owner) repositories: (repo),
}

edge fork {
    (fork) origin: (origin)
}

def RepositoryOwner (
    rel* is?: User
    rel* is?: Organization
)

def User (
    rel. 'id': (fmt '' => 'user/' => text => .)
    rel* 'email': text
    rel* membership.member_of: {Organization}
    rel* ownership.repositories: {Repository}
)

def Organization (
    rel. 'id': (fmt '' => 'org/' => text => .)
    rel* membership.members: {User}
    rel* ownership.repositories: {Repository}
)

edge membership {
    (org) members: (member) membership: Membership,
    (member) member_of: (org) membership: Membership,
}

def Membership (
    rel* 'role': text
)

map users(
    (
        'ids'?: { ..id },
        'member_of'?: { ..member_of }
    ),
    User {
        ..@match User(
            'id'?: @in { ..id },
            membership.member_of?: Organization (
                'id': @in { ..member_of }
            ),
        ),
    }
)

map organizations(
    (),
    Organization {
        ..@match Organization(),
    }
)

map repositories(
    (),
    Repository {
        ..@match Repository(),
    }
)
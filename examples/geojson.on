/// A position is the fundamental geometry construct.
type position {
    rel _ 0..2: int
}

/// A list of two positions or more.
type position-list {
    rel _ ..2: position
    rel _ 2..: position
}

/// To specify a constraint specific to Polygons, it is useful to
/// introduce the concept of a linear ring:
///
/// A linear ring is a closed LineString with four or more positions.
///
/// * The first and last positions are equivalent, and they MUST contain
///   identical values; their representation SHOULD also be identical.
/// * A linear ring is the boundary of a surface or the boundary of a
///   hole in a surface.
/// * A linear ring MUST follow the right-hand rule with respect to the
///   area it bounds, i.e., exterior rings are counterclockwise, and
///   holes are clockwise.
///
type position-ring {
    rel _ ..4: position
    rel _ 4..: position
}

/// For type "Point", the "coordinates" member is a single position.
pub type Point {
    rel _ 'type': 'Point'
    rel _ 'coordinates': position
}

/// For type "MultiPoint", the "coordinates" member is an array of
/// positions.
pub type MultiPoint {
    rel _ 'type': 'MultiPoint'
    rel _ ['coordinates']: position
}

/// For type "LineString", the "coordinates" member is an array of two or
/// more positions.
pub type LineString {
    rel _ 'type': 'LineString'
    rel _ 'coordinates': position-list
}

/// For type "MultiLineString", the "coordinates" member is an array of
/// LineString coordinate arrays.
pub type MultiLineString {
    rel _ 'type': 'MultiLineString'
    rel _ ['coordinates']: position-list
}

/// For type "Polygon", the "coordinates" member MUST be an array of
/// linear ring coordinate arrays.
pub type Polygon {
    rel _ 'type': 'Polygon'
    rel _ 'coordinates': position-ring
}

/// For type "MultiPolygon", the "coordinates" member is an array of
/// Polygon coordinate arrays.
pub type MultiPolygon {
    rel _ 'type': 'MultiPolygon'
    rel _ ['coordinates']: position-ring
}

/// LeafGeometry is a union of geometries that does not include `GeometryCollection`.
pub type LeafGeometry {
    rel _ is?: Point
    rel _ is?: MultiPoint
    rel _ is?: LineString
    rel _ is?: MultiLineString
    rel _ is?: Polygon
    rel _ is?: MultiPolygon
}


/// A GeoJSON object with type "GeometryCollection" is a Geometry object.
/// A GeometryCollection has a member with the name "geometries".  The
/// value of "geometries" is an array.  Each element of this array is a
/// GeoJSON Geometry object.  It is possible for this array to be empty.
pub type GeometryCollection {
    rel _ 'type': 'GeometryCollection'
    rel _ ['geometries']: LeafGeometry
}

/// A Geometry object represents points, curves, and surfaces in
/// coordinate space.  Every Geometry object is a GeoJSON object no
/// matter where it occurs in a GeoJSON text.
///
/// Note: Geometry is an extension of LeafGeometry + GeometryCollection.
pub type Geometry {
    rel _ is?: Point
    rel _ is?: MultiPoint
    rel _ is?: LineString
    rel _ is?: MultiLineString
    rel _ is?: Polygon
    rel _ is?: MultiPolygon
    rel _ is?: GeometryCollection
}

type GeometryOrNull {
    // TODO: Not supported yet (union tree):
    rel _ is: Geometry
    // rel _ is?: ()
}

/// A Feature object represents a spatially bounded thing.  Every Feature
/// object is a GeoJSON object no matter where it occurs in a GeoJSON
/// text.
type Feature<properties = ()>
rel Feature<> 'type': 'Feature'
rel Feature<> 'geometry': GeometryOrNull

// FIXME: Features is a map of 'anything'.
// This could be a good use case for generics in ONTOL.
rel Feature<properties> 'properties': ()

/// A GeoJSON object with the type "FeatureCollection" is a
/// FeatureCollection object.  A FeatureCollection object has a member
/// with the name "features".  The value of "features" is a JSON array.
/// Each element of the array is a Feature object as defined above.  It
/// is possible for this array to be empty.
type FeatureCollection<properties = ()>
rel FeatureCollection<> 'type': 'FeatureCollection'
rel FeatureCollection<properties> ['features']: Feature<properties>

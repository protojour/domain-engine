use 'geojson' as geo

def TramState (
    rel .is?: 'in operation',
    rel .is?: 'in maintenance',
)

def Facility (
    rel .is?: 'toilet',
    rel .is?: 'ticket sales',
)

def AccessibilityTags (
    rel .is?: 'stairs'
    rel .is?: 'lift'
    rel .is?: 'raised curb'
    rel .is?: 'wheelchair ramp'
)

def StopPlace (
    rel .'id'|id: { fmt '' => 'NSR:StopPlace:' => i64 }
    rel .'name': text
    rel .'location': geo.Point
    rel .'facilities': {Facility}
    rel .'a11y_tags': {AccessibilityTags}
)

def Line (
    rel .'id'|id: { rel .is: text }
    rel .'direction': text
)

def TramCarriage (
    rel .'id'|id: ( rel .is: text )
    rel .'model': text
    rel .'state': TramState
    rel .'last_service': datetime
    rel .'max_passengers': i64
    rel .'seats': i64
    rel .'a11y_tags': {AccessibilityTags}
)

def RoutePoint (
    rel .'id'|id: ( rel .is: uuid )
    rel .'line': Line
    rel .'stop_place': StopPlace
    rel .'time': text
)

def TrackSegment (
    rel .'length': i64
    rel .'average_tram_traversal_time': i64
)

def LineSegment (
    rel .is: TrackSegment
    rel .'line': Line
)

rel StopPlace 'connection'(rel .is: LineSegment): {StopPlace}

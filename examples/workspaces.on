/// Memoriam core domain modelling frequently changing code repositories.
///
/// Workspaces are version-controlled trees of files, where the data is represented by conflict-free replicated data types (CRDTs).
domain 01JAP41VG1STK1VZPWXV26SPNM (
    rel. name: 'workspaces'
)

/// A workspace is a version-controlled tree of files associated with an ID.
def Workspace (
    /// The ULID of this workspace
    rel. 'id'[rel* gen: auto]: (rel* is: ulid)

    /// The conflict-free replicated representation of the workspace data
    rel* 'data'[rel* repr: crdt]: WorkspaceData

    /// When this workspace got created
    rel* 'created_at'[rel* gen: create_time]: datetime

    /// When this workspace was last updated
    rel* 'updated_at'[rel* gen: update_time]: datetime

    /// An optional, descriptive title for this workspace.
    /// It can be any text, and does not need to be unique.
    rel* 'title'?: text

    rel* order[
        rel* 0: 'updated_at'
        rel* direction: descending
    ]: latest_update
)

/// The data making up a workspace.
def WorkspaceData (
    /// NOTE: Deprecated. Do not delete it (yet), since subsequent props will change property ID, need a solution for that.
    /// This is a good case study for writing an initial migration system for ONTOL domains.
    rel* 'title'?: text

    /// The files that workspace contains
    rel* 'files': {File}
)

/// A file in a workspace.
def File (
    /// The path of the file.
    /// To simulate directories, use forward slashes to introduce hierarchies.
    rel* 'path': /^[\w\.]+(\/[\w\.]+)*$/

    /// the contents of the file
    rel* 'contents': text
)

/// A listing of a workspace without the data
def WorkspaceListing (
    rel* 'id': ulid
    rel* 'created_at': datetime
    rel* 'updated_at': datetime
    rel* 'title'?: text
)

/// Symbols for named orderings
sym { latest_update }

map(
    WorkspaceListing(
        'id': id,
        'created_at': ca,
        'updated_at': ua,
        'title'?: title
    ),
    @match Workspace(
        'id': id,
        'created_at': ca,
        'updated_at': ua,
        'title'?: title
    ),
)

map listMostRecentWorkspaces(
    (),
    WorkspaceListing {
        ..@match Workspace(
            order: { latest_update() }
        )
    }
)

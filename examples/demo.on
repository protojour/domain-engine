// -----------------------------------
// database

type archive_item_id {
    fmt '' => 'archive_item/' => uuid => .
}
type archive_group_id {
    fmt '' => 'archive_group/' => uuid => .
}
type item_type {
    rel .is?: 'worthless junk'
    rel .is?: 'ancient treasure'
    rel .is?: 'alien artifact'
}
type archive_item {
    rel archive_item_id identifies: .
    rel .'created': datetime
    rel .'updated': datetime
    rel .'archived': datetime
    rel .'name': string
    rel .'description': string
    rel .'asset_score': int
    rel .'type': item_type
}
type archive_group {
    rel archive_group_id identifies: .
    rel .'created': datetime
    rel .'updated': datetime
    rel .'archived': datetime
    rel .'name': string
    rel .'description': string
    rel .'start_date': datetime
    rel .'end_date': datetime
    rel [.] 'archive_items'::'archive_groups' [archive_item]
}

// -----------------------------------
// public API

/// ID for an Archive Object
pub type ArchiveObjectID {
    fmt '' => 'archive_item/' => uuid => .
}
/// ID for an Archive Collection
pub type ArchiveCollectionID {
    fmt '' => 'archive_group/' => uuid => .
}
/// Common dates for Archive entities
type ArchiveDates {
    rel .'createdAt': datetime
    rel .'updatedAt': datetime
    rel .'archivedAt': datetime
}
/// An Archived Object
pub type ArchiveObject {
    rel ArchiveObjectID identifies: .
    rel .'dates': ArchiveDates
    rel .'name': string
    rel .'description': string
    rel .'assetScoreSquared': int
    rel .'kind': item_type
}
/// An Archived Object Collection
pub type ArchiveCollection {
    rel ArchiveCollectionID identifies: .
    rel .'dates': ArchiveDates
    rel .'name': string
    rel .'description'?: string
    rel .'startDate': datetime
    rel .'endDate': datetime
    rel [.] 'archiveItems'::'archiveGroups' [ArchiveObject]
}

map {
    archive_item {
        'created': cr
        'updated':  up
        'archived': ar
        'name': n
        'description': d
        'asset_score': s
        'type': t
    }
    ArchiveObject {
        'dates': ArchiveDates {
            'createdAt': cr
            'updatedAt': up
            'archivedAt': ar
        }
        'name': n
        'description': d
        'assetScoreSquared': s * s
        'kind': t
    }
}
